### CPU.hdl
* code
// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * Hack CPU（中央處理單元），由ALU組成，
 * 兩個名為A和D的寄存器以及一個名為PC的程序計數器。
 * CPU旨在讀取並執行編寫的指令
 * t哈克機器語言。具體來說，功能如下：
 * 根據Hack機器執行輸入的指令
 * 語言規範。語言規範中的D和A
 * 是指CPU駐留寄存器，而M是指外部寄存器
 * 由A尋址的存儲位置，即到Memory [A]。inM輸入
 * 保存此位置的值。如果當前指令需要
 * 向M寫一個值，該值放在地址outM中
 * 目標位置的放置在addressM輸出中，並且
 * writeM控制位有效。（當writeM == 0時，任何值都可以
 * 出現在outM中）。outM和writeM輸出是組合的：
 * 它們受當前執行的瞬間影響
 * 指令。addressM和pc輸出是時鐘：儘管它們
 * 受當前指令執行的影響，他們提交
 * 僅在下一個步驟中恢復到新值。如果reset == 1，則
 * CPU跳到地址0（即在下一個時間步中將pc設置為0），而不是
 * 而不是執行當前指令所得到的地址。 
 */

### 以下是老師寫的範例

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // A register
    Not(in=instruction[15], out=isA);         // isA = not I15
    Not(in=isA, out=isC);                     // isC = I15
    And(a=isC, b=instruction[5], out=AluToA); // AluToA = isC & d1
    Or(a=isA, b=AluToA, out=Aload);
    
    Mux16(a=instruction, b=ALUout, sel=isC, out=Ain);
    ARegister(in=Ain, load=Aload, out=Aout);
    
    // D register
    And(a=isC, b=instruction[4], out=Dload);  // Dload = isC & d2
    DRegister(in=ALUout, load=Dload, out=Dout);
    
    // ALU
    Mux16(a=Aout, b=inM, sel=instruction[12], out=AorM); // cAorM = I[12]
    
    ALU(x=Dout, y=AorM, zx=instruction[11], nx=instruction[10], 
        zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], 
        out=ALUout, zr=zr, ng=ng);
    
    PC(in=Aout, load=PCload, inc=true, reset=reset, out[0..14]=pc);
    
    // JUMP condition
    Or(a=ng, b=zr, out=ngzr);			                    // ngzr = (ng | zr)
    Not(in=ngzr, out=g);			                        // g = out > 0 = !(ng | zr);  ng = out < 0;  zr = out = 0
    And(a=ng, b=instruction[2], out=passLT);          // ngLT = (ng & LT)
    And(a=zr, b=instruction[1], out=passEQ);          // zrEQ = (zr & EQ)
    And(a=g,  b=instruction[0], out=passGT);          // gGT = (g & GT)
    Or(a=passLT, b=passEQ, out=passLE);
    Or(a=passLE, b=passGT, out=pass);

    And(a=isC, b=pass, out=PCload); // PCload = isC & J

    // output
    And16(a=Aout, b=Aout, out[0..14]=addressM);
    And(a=isC, b=instruction[3], out=writeM); // writeM = isC & d3
    And16(a=ALUout, b=ALUout, out=outM);
}

